import groovy.json.JsonSlurper
import org.apache.tools.ant.taskdefs.condition.Os
import org.codehaus.groovy.runtime.MethodClosure

def readModInfoJson() {
    def jsonFile = file('src/main/resources/mod_info.json')
    def slurper = new JsonSlurper()
    return slurper.parse(jsonFile)
}

def areGradlePropertiesDefined() {
    return (project.hasProperty('game.dir') && project.hasProperty('game.mod_folder'))
}

def checkPropertiesWithWarning() {
    if (areGradlePropertiesDefined()) return true
    println 'WARNING: Missing properties in gradle.properties. See gradle.properties.dist as an example.'
    println 'WARNING: Task execution stopped.'
    return false
}

def getStarsectorModDir() {
    if (!areGradlePropertiesDefined()) return ""
    return project.getProperty('game.dir') + project.getProperty('game.mod_folder') + readModInfoJson().id
}

def getStarsectorExecutableDir() {
    if (!areGradlePropertiesDefined()) return ""
    return Os.isFamily(Os.FAMILY_WINDOWS) ? file(project.getProperty('game.dir') + 'starsector-core') : file(project.getProperty('game.dir'))
}

ext {
    readModInfoJson = this.&readModInfoJson as MethodClosure
    arePropertiesDefined = this.&checkPropertiesWithWarning as MethodClosure
    getStarsectorModDir = this.&getStarsectorModDir as MethodClosure
    getStarsectorExecutableDir = this.&getStarsectorExecutableDir as MethodClosure
}
