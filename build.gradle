import groovy.json.JsonSlurper
import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id 'java'
    id 'eclipse'
}

repositories {
    mavenCentral()
}

// fix documentation
eclipse {
    classpath {
        file {
            whenMerged {
                def lib = entries.find { it.path.contains 'starfarer.api.jar' }
                lib.javadocPath = fileReference(file('libs/starfarer.api.zip'))
                lib.sourcePath = fileReference(file('libs/starfarer.api.zip'))
            }
        }
    }
}

// project level config
sourceCompatibility = 1.7
targetCompatibility = 1.7

// build everything and make a jar
build {
    doLast {
        copy {
            from jar
            into '.'
        }
    }
}

// compile time dependencies
dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation group: 'com.thoughtworks.xstream', name: 'xstream', version: '1.4.10'
    implementation group: 'org.lwjgl.lwjgl', name: 'lwjgl', version: '2.9.3'
    implementation group: 'org.lwjgl.lwjgl', name: 'lwjgl_util', version: '2.9.3'
    implementation group: 'log4j', name: 'log4j', version: '1.2.9'
    implementation group: 'org.json', name: 'json', version: '20170516'
    implementation group: 'net.java.jinput', name: 'jinput', version: '2.0.7'
    implementation group: 'org.codehaus.janino', name: 'janino', version: '3.0.7'

    // lombok
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.20'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'

    // test only
    testImplementation 'junit:junit:4.12'
}


task release(type: Zip) {
    dependsOn build
    archiveFileName = "${project.name}-${modInfo().version}.zip"
    destinationDirectory = file(rootDir)
    from sourceSets.main.resources
    from jar
    into project.name
}

def modInfo() {
    def jsonFile = file('src/main/resources/mod_info.json')
    def slurper = new JsonSlurper()
    return slurper.parse(jsonFile)
}

def getModDir() {
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        return project.getProperty('game.dir') + "\\mods\\" + modInfo().id
    }
    return project.getProperty('game.dir') + "/mods/" + modInfo().id
}

task copyToMods(type: Copy) {
    if (!project.hasProperty('game.dir')) {
        println "WARNING: Missing game.dir in gradle.properties. See gradle.properties.dist as an example."
        println "WARNING: Task execution stopped."
        return;
    }

    dependsOn build
    from jar

    into getModDir()
}

def getCoreDir() {
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        return project.getProperty('game.dir') + "\\starsector-core\\"
    }
    return project.getProperty('game.dir') + "/starsector-core/"
}

task runGameWithBuild(type:Exec) {
    dependsOn copyToMods

    workingDir = file(getCoreDir())

    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        commandLine 'cmd', '/c', 'starsector.bat'
    } else {
        commandLine 'sh', './starsector.command'
    }
}
